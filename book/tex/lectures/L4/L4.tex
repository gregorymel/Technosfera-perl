\setcounter{chapter}{3}
\chapter{Unicode и регулярные выражения}
Данные темы, Unicode и регулярные выражения, относятся в большей части не конкретно к языку Perl, а к программированию в целом: данная лекция будет полезна независимо от выбора языка для разработки.

\section{Unicode} %1 0:50
Unicode --- стандарт кодирования символов, позволяющий представить знаки практически всех письменных языков. Каждый символ идентифицируется уникальным кодом, но этот код может быть представлен по-разном в виде различных последовательностей байт.

Unicode Transformation Format (Формат преобразования юникода)~--- способ представления символов Unicode в виде последовательности целых положительных чисел. Наиболее распространенные кодировки:
\begin{itemize}
  \item UTF-8 (8-битный) endianness safe
  \item UTF-16 (16-битный) LE | BE
  \item UTF-32 (32-битный) LE | BE
\end{itemize}
Кодировка utf8 позволяет кодировать символы в следующих диапазонах:
\begin{verbatim}
  Code Points   Bytes: 1st    2nd    3rd    4th

   U+0000..U+007F     00..7F
   U+0080..U+07FF     C2..DF 80..BF
   U+0800..U+0FFF     E0     A0..BF 80..BF
   U+1000..U+CFFF     E1..EC 80..BF 80..BF
   U+D000..U+D7FF     ED     80..9F 80..BF
   U+D800..U+DFFF     utf16 surrogates, not utf8
   U+E000..U+FFFF     EE..EF 80..BF 80..BF
  U+10000..U+3FFFF    F0     90..BF 80..BF 80..BF
  U+40000..U+FFFFF    F1..F3 80..BF 80..BF 80..BF
 U+100000..U+10FFFF   F4     80..8F 80..BF 80..BF
\end{verbatim}
Начальный диапазон совпадает с ASCII (за что ее и полюбили) и в нем длина записываемого символа равна одному байту. Чем дальше расположен код, тем большее число байт необходимо, чтобы его закодировать.

\section{Unicode в perl}  %10 3:42
В perl выделяют следующие понятия:
\begin{itemize}
  \item Символ --- любой unicode-символ:
  \begin{minted}{perl}
    "\x{1}" .. "\x{10FFFF}"
    chr(1)  .. chr(0x10FFFF)
  \end{minted}
  \item Байт (октет) --- символ с кодом до 255:
  \begin{minted}{perl}
    "\x00" .. "\xff"
    "\000" .. "\377"
    chr(0) .. chr(255)
  \end{minted}
\end{itemize}

Обычно создается байтовая строка:
\begin{minted}{perl}
my $bytes = "123";
printf "%vX", $bytes; # 31.32.33
my $bytes = "\001\002\377";
printf "%vX", $bytes; # 1.2.ff
my $bytes = "\xfe\xff";
printf "%vX", $bytes; # fe.ff
\end{minted}
Если подключить определенную директиву, можно создавать строку из символов:
\begin{minted}{perl}
use utf8;
my $string = "Ёлка";#\x{401}\x{43b}\x{43a}\x{430}
printf "%vX", $string; # 401.43B.43A.430
my $string = "\x{0401}";
printf "%vX", $string; # 0401 
\end{minted}
Преобразования между этими двумя представлениями строк это:
\begin{itemize}
    \item \textbf{encoding} --- преобразование текста (строк, символов) в данные (байты, октеты)
    \item \textbf{decoding} -- преобразование данных (байт, октетов) в текст (строки символов)
\end{itemize}
С помощью штатного модуля Encode можно делать такие преобразования:
\begin{minted}{perl}
use Encode qw(encode decode);

my $bin = "\320\201";
printf "%vX", $bin; # D0.81

my $str = decode("utf-8", $bin); # "\x{0401}"
printf "%vX",$str; # 0401

my $bin = encode("utf-8", $str); # "\320\201"
printf "%vX", $bin; # D0.81

my $bytes_dos = "\xf1"; # cp866 ё
printf "%vX", $bytes_dos; # F1
my $chars = decode("cp866",$bytes_dos);
my $bytes_win = encode("cp1251", $chars);
printf "%vX", $bytes_win; # B8

my $to = encode("cp1251",decode("cp866",$from));
from_to($from,"cp866","cp1251"); # inplace
\end{minted}
Для того, чтобы преобразовать одну кодировку к другой, сначала необходимо сделать decode из одной кодировки, а потом encode в другую.

Perl различает строки от октетов с помощью так называемого utf8 flag (введен в версии 5.8). У каждой переменной, которая является строкой, есть внутренний атрибут, который говорит, является ли она строкой или представлением байт. Функция \verb|utf8::is_utf8| возвращает true или false в зависимости от этого.
\begin{minted}[escapeinside=||]{perl}
say utf8::is_utf8("\320\201"); # ''
my $string = decode("utf-8", "\320\201");
say utf8::is_utf8($string); # 1

say utf8::is_utf8("\x{0401}"); # 1
my $octets = encode("utf-8", "\x{0401}");
say utf8::is_utf8($octets); # ''

printf "U+%v04X\n", decode('utf8',"тест");
# U+0442.0435.0441.0442

say utf8::is_utf8("Ё"); # ''

printf "U+%v04X\n", "Ё";
# U+00D0.0081
\end{minted}

Здесь с помощью специальной последовательности \verb|"U+%v04X\n"| (которую нужно передать функции printf) был выведен шестнадцатеричный код остальных символов. Это полезно для отладки.

С помощью директивы use utf8 можно сообщить Perl, что необходимо декодировать весь текст в файле с исходным кодом программы. Если там содержатся unicode-строки, то на них автоматом взводится utf8 flag:
\begin{minted}{perl}
use utf8;

say utf8::is_utf8("\320\201"); # ''

say utf8::is_utf8("\x{0401}"); # 1

say utf8::is_utf8("Ё"); # 1
\end{minted}
С помощью модуля DevelL::Peek можно увидеть внутреннее устройство переменных в perl (о нем подробнее речь пойдет позже):
\begin{minted}{bash}
$ perl -MDevel::Peek -E 'Dump "Ё"'
SV = PV(0x7fad0a006540) at 0x7fad0a02d700
  REFCNT = 1
  FLAGS = (POK,IsCOW,READONLY,PROTECT,pPOK)
  PV = 0x7fad09d024d0 "\320\201"\0
  CUR = 2
  LEN = 10
\end{minted}
Так можно просмотреть содержимое переменной с включенной директивой utf8:
\begin{minted}{bash}
$ perl -MDevel::Peek -Mutf8 -E 'Dump "Ё"'
SV = PV(0x7fbaa8806560) at 0x7fbaa882d790
  REFCNT = 1
  FLAGS = (POK,IsCOW,READONLY,PROTECT,pPOK,UTF8)
  PV = 0x7fbaa8404fe0 "\320\201"\0 [UTF8 "\x{0401}"]
  CUR = 2
  LEN = 10
\end{minted}
У переменной появился флаг utf8.

Существует еще одна особенность: если код символа меньше 255, флаг utf8 не взводится автоматически:
\begin{minted}{bash}
$ perl -MDevel::Peek -E 'Dump "\x{ff}"'
SV = PV(0x7fa153802948) at 0x7fa153005b00
  REFCNT = 1
  FLAGS = (PADTMP,POK,READONLY,pPOK)
  PV = 0x7fa152d06a10 "\377"\0
  CUR = 1
  LEN = 16
\end{minted}
Если код становится больше, utf8 флаг вместе с этим взводится.
\begin{minted}{bash}
$ perl -MDevel::Peek -E 'Dump "\x{100}"'
SV = PV(0x7fcdbc003548) at 0x7fcdbc02c100
  REFCNT = 1
  FLAGS = (PADTMP,POK,READONLY,pPOK,UTF8)
  PV = 0x7fcdbb707110 "\304\200"\0 [UTF8 "\x{100}"]
  CUR = 2
  LEN = 16
\end{minted}
Встроенные функции, в отличие от операторов, полагаются на наличие флага utf8. Они не смотрят на окружения и директивы. Например:\\
\begin{minted}{perl}
my $test = "тест";
say length $test;
say uc $test;
say utf8::is_utf8 $test;
say ord(substr($test,0,1));
printf "%vX", $test;
\end{minted}
\begin{minted}{perl}
#
8
тест
''
209
D1.82.D0.B5.D1.81.D1.82
\end{minted}

В свою очередь:
\begin{minted}{perl}
use utf8;
my $test = "тест";
say length $test;
say uc $test;
say utf8::is_utf8 $test;
say ord(substr($test,0,1));
printf "%vX", $test;
\end{minted}
\begin{minted}{perl}
#
#
4
ТЕСТ
1
1090 # 0x442
442.435.441.442
\end{minted}

Если необходимо передать в качестве входящего аргумента utf8-строку, то чтобы у нее установился utf8 флаг, необходимо либо передать параметр A ключу -C (который отвечает за поведение юникода):
\begin{minted}{bash}
$ perl -CA ...
\end{minted}
Либо установить соответствующее значение для переменной окружения \verb|PERL_UNICODE|:
\begin{minted}{bash}
$ export PERL_UNICODE=A
\end{minted}
Еще один способ сделать то же самое --- написать в программе (декодировать \verb|@ARGV|):
\begin{minted}{perl}
use Encode qw(decode_utf8);
BEGIN {
    @ARGV = map { decode_utf8($_, 1) } @ARGV;
}
\end{minted}
По умолчанию \verb|STDIN|, \verb|STDOUT|, \verb|STDERR| не являются UTF-8. Другими словами, считанный из файла текст не будет строкой, а при записи UTF-8-строки в stdout или stderr будет выведено <<Wide character in print at...>>. В этом случае необходимо указать, что конкретный дескриптор является дескриптором с UTF-8:
\begin{minted}{bash}
$ perl -CS ...
$ export PERL_UNICODE=S
\end{minted}
Также это может быть сделано вручную на любом дескрипторе при помощи дополнительного слоя <<:utf8>> (подробнее про слои будет рассказано позже в курсе):
\begin{minted}{perl}
binmode(STDOUT,':utf8');
open my $f, '<:utf8', 'file.txt';
use open qw(:std :utf8); # auto
\end{minted}
С помощью модуля open можно установить такое поведение в качестве поведения по умолчанию. Сделать весь ввод/вывод в UTF-8 можно следующими способами. С помощью ключа или нескольких ключей:
\begin{minted}{perl}
$ perl -CASD ... | perl -CS -CA -CD ...
\end{minted}
Также можно соответствующим образом установить переменную окружения:
\begin{minted}{perl}
$ export PERL_UNICODE=ASD
\end{minted}
И наконец:
\begin{minted}{perl}
use open qw(:std :utf8);
use Encode qw(decode_utf8);
BEGIN{ @ARGV = map decode_utf8($_, 1),@ARGV; }
\end{minted}
После этого программа будет читать в unicode, писать в unicode и работать внутри себя в unicode. Чтобы в таком режиме работать с бинарными строками (например открыть бинарный файл), можно использовать слой ввода/вывода raw:
\begin{minted}{perl}
binmode($fh,':raw');

binmode(STDOUT,':raw');

open my $f, '<:raw', 'file.bin';

my $socket = accept(...);
binmode($socket,':raw');
\end{minted}
В Perl ставится во главу угла поддержка максимального количества возможностей unicode. Нововведения стараются внедрить как можно быстрее, поэтому среди всех языков программирования наилучшую поддержку unicode обеспечивает именно Perl.

Например, для последовательности на известном или неизвестном Вам языке можно применить фонетическую транслитерацию с помощью модуля Unidecode:
\begin{minted}{perl}
use utf8;
use Text::Unidecode;

say unidecode "\x{5317}\x{4EB0}"; # 北亰
# That prints: Bei Jing

say unidecode "Это тест";
# That prints: Eto tiest
\end{minted}
Будет выведена английская фонетическая транслитерация.

Модуль charnames позволяет работать с полными названиями символов:
\begin{minted}{perl}
use charnames qw(:full :short latin greek);
say "\N{MATHEMATICAL ITALIC SMALL N}"; # 𝑛
say "\N{GREEK CAPITAL LETTER SIGMA}"; # Σ
say "\N{Greek:Sigma}"; # Σ
say "\N{ae}"; # æ
say "\N{epsilon}"; # ε
say "\N{LATIN CAPITAL LETTER A WITH MACRON AND GRAVE}";
$s = "\N{Latin:A WITH MACRON AND GRAVE}";
say $s;  # Ā̀
printf "U+%v04X\n", $s; # U+0100.0300
\end{minted}
Некоторые символы могут быть на самом деле сборными (например Ā̀  состоит из двух символов: символа A и символа, обозначающего диакритический знак). Также можно регистрировать последовательности для символом (торговые марки не вносятся в таблицу симовлов):
\begin{minted}{perl}
use charnames ":alias" => {
    "APPLE LOGO" => 0xF8FF,
};
say "\N{APPLE LOGO}"; # 
\end{minted}

Следующая замечательная особенность unicode заключается в том, для одной буквы может существовать множество вариантов начертаний (case). Для того, чтобы удобно сравнивать строки (не учитывать case), существует специальная форма букв --- foldcase. Начиная с Perl v5.16 доступна функция fc:
\begin{minted}{perl}
use feature "fc"; # perl v5.16+

# sort case-insensitively
my @sorted = sort {
    fc($a) cmp fc($b)
} @list;

# both are true:
fc("tschüß") eq fc("TSCHÜSS")
fc("Σίσυφος") eq fc("ΣΊΣΥΦΟΣ")
\end{minted}
На этом знакомство с unicode в Perl закончено.

\section{Регулярные выражения} %27 20:19
\subsection{Регулярные выражения. Пример <<Credit card numbers>>}
Регулярные выражения ---  одна из самых значимых частей языка perl, которые были с самого начала и с тех пор только развивались. Регулярные выражения из Perl были выделены в отдельной библиотеки <<perl compatible regular expression>> (pcre) для использования в других языках.

Регулярные выражения --- формальный язык поиска и осуществления манипуляций с подстроками в тексте, основанный на использовании метасимволов.

Например следующее регулярное выражение:
\begin{minted}{perl}
^(?:
    4[0-9]{12}(?:[0-9]{3})?          # Visa
|
    5[1-5][0-9]{14}                  # MC
|
    3[47][0-9]{13}                   # AmEx
|
    3(?:0[0-5]|[68][0-9])[0-9]{11}   # Diners
|
    6(?:011|5[0-9]{2})[0-9]{12}      # Discover
|
    (?:2131|1800|35\d{3})\d{11}      # JCB
)$
\end{minted}
находит в любом тексте номера кредитных карт.

\subsection{Операторы в регулярных выражениях} %31 23:03
Самое простое регулярное выражение --- сопоставление, то есть проверка, есть ли в строке подстрока:
\begin{minted}{perl}
"hello" =~ /hell/; # matches
"1+2" =~ /1+2/;  # not, "12" or "112" will match
"1+2" =~ /1\+2/; # matches
"1+2" =~ /\d\+\d/; # matches
"/my/path" =~ m"/path" # match /path

"bat" =~ /[bcr]at/; # matches
"cat" =~ /[bcr]at/; # matches
"rat" =~ /[bcr]at/; # matches
"fat" =~ /[bcr]at/; # not
"at"  =~ /[bcr]at/; # not
\end{minted}
В случае использование разделителей /.../ использование m не обязательно. В остальных случаях необходимо писать m:
\begin{minted}{perl}
my $string = "sample string";

$string =~  /sample/;
$string =~ m/sample/;
$string =~ m(sample);

my @a = $string =~ /sample/; # list of caps
my $a = $string =~ /sample/; # true|false
if ($string =~ /sample/) # also boolean
   { ... }

for (@samples) {
    /sample/;
    # same as $_ =~ /sample/;
    if (m/sample/) { ... }
    # if ($_ =~ /sample/) { ... }
}
\end{minted}
Сопоставление работает как в списковом контексте (возвращает совпадение), так и скалярном (возвращает true или false). Без указания операнда оператор работает над переменной \verb|$_|.

Следующий оператор --- поиск и замена \verb|s///|:
\begin{minted}{perl}
my $string = "sample string";

$string =~ s/sample/item/;
$string =~ s{sample}{item};
$string =~ s{sample}
            (item);

my $count_of_replace =
    $string =~ s{sample}{item}g;

for (@samples) {
    s/sample/item/;
    # $_ =~ /sample/item/;
}
\end{minted}
По умолчанию в скалярном контексте возвращается количество совершенных замен. Без указания операнда оператор работает над переменной \verb|$_|. Оператор поиска и замены модифицирует ту строку, над которой применяется.


Следующий оператор --- транслитерация (\verb|y///|, \verb|tr///|):
\begin{minted}{perl}
my $str = "MiXeD CaSe StRiNg";

# ASCII lowercase;
$str =~ tr/A-Z/a-z/;
# mixed case string

# Change case
my $str = "MiXeD CaSe StRiNg";
$str =~ tr/A-Za-z/a-zA-Z/;
# mIxEd cAsE sTrInG

# ROT-13
$str =~ tr/A-Za-z/N-ZA-Mn-za-m/;
# zVkRq pNfR fGeVaT
\end{minted}
Это специальное выражение относится к регулярным выражениям по синтаксису, но как таковым выражением не является. Этот оператор проходит по всей строке и ищет в ней символы из левой части. Если таковой встречается, он заменяется на соответствующий символ из правой части.


\subsection{Метасимволы, классы символов и квантификаторы} %34 28:49
Полный список метасимволов, которые используются внутри регулярных выражений:
\begin{verbatim}
{ } [ ] ( ) ^
$ . | * + ? \
\end{verbatim}
Чтобы использовать их как символы, их нужно экранировать. Все остальные символы экранировать не нужно.

Классы символов позволяют указать, какие символы должны стоять на конкретной позиции:
\begin{minted}{perl}
[...]      # перечисление
/[abc]/      # "a" или "b" или "c"
/[a-c]/      # то-же самое
/[a-zA-Z]/   # ASCII алфавит

/[bcr]at/    # "bat" или "cat" или "rat"

[^...]     # отрицательное перечисление
/[^abc]/     # что угодно, кроме "a", "b", "c"
/[^a-zA-Z]/  # что угодно, кроме букв ASCII
\end{minted}
Также существуют заранее определенные классы символов:
\begin{verbatim}
`\d` - цифры. не только `[0-9]` # ۰ ۱ ۲ ۳ ۴ ۵
`\s` - пробельные символы `[\ \t\r\n\f]` и др.
`\w` - "буква". `[0-9a-zA-Z_]` и юникод

`\D` - не цифра. `[^\d]`
`\S` - не пробельный символ. `[^\s]`
`\W` - не "буква". `[^\w]`

`\N` - что угодно, кроме "\n"
`.`  - что угодно, кроме "\n" ⃰
`^`  - начало строки ⃰ ⃰
`$`  - конец строки ⃰ ⃰

>∗  поведение меняется в зависимости от модификатора /s
>∗∗ поведение меняется в зависимости от модификатора /m
\end{verbatim}

Квантификатор после символа, символьного класса или группы определяет, сколько раз предшествующее выражение может встречаться:
\begin{itemize}[nosep]
 \item ? --- 0 или 1 ({0,1})
 \item * --- 0 или более ({0,})
 \item + --- 1 или более ({1,})
 \item {x} --- ровно x
 \item {x,y} --- от x до y включительно
 \item {,y} --- от 0 до y включительно
 \item {x,} --- от x до бесконечности*
\end{itemize}
Здесь бесконечность считается равной 32768.

\begin{minted}{perl}
/^1?$/  # "" or "1"
/^a*$/  # "" or "a", "aa", "aaa", ...
/^\d*$/ # "" or "123", "11111111", ...
/^.+$/  # "1" or "abc", not ""

/^\d{4}-\d{2}-\d{2} \d{2}:\d{2}:\d{2}$/
	# "2015-10-14 19:35:01"
\end{minted}

\subsection{Захваты, группы и оглядывания} %39 %36:00
Захваты --- специальные переменные
\begin{verbatim}
> `$1`, `$2`, `$3`, ...
\end{verbatim}
в которые попадает то, что регулярном выражении записано в круглых скобках:
\begin{minted}{perl}
$_ = "foo bar baz";

m/^(\w+)\s+(\w+)\s+(\w+)$/;
# $1 = 'foo';
# $2 = 'bar';
# $3 = 'baz';

m/^(\w(\w+))\s+((\w+))/;
#  1  2        34
# $1 = 'foo';
# $2 = 'oo';
# $3 = 'bar';
# $4 = 'bar';
\end{minted}
Нумерация переменных выполняется в той последовательности, в какой появляется открывающая скобка соответствующего блока.

Круглые скобки являются самой простой захватывающей группой.
\begin{itemize}[nosep]
  \item (...) --- захватывающая группа
  \item (?:...) --- незахватывающая группа
\end{itemize}

\begin{minted}{perl}
"a" =~ /^(?:a|b|cd)$/;   # match
"b" =~ /^(?:a|b|cd)$/;   # match
say $1; # undef
"ax" =~ /^(?:a|b|cd)$/;  # no match

"a" =~ /^(a|b|cd)$/;   # match
say $1; # a
"b" =~ /^(a|b|cd)$/;   # match
say $1; # b
"ax" =~ /^(a|b|cd)$/;  # no match
say $1; # undef
\end{minted}

\begin{itemize}[nosep]
  \item (?<name>...) или (?'name'...) --- захватывающая именованная группа.
\end{itemize}
Значения соответствующих захватов попадают в специальный хеш \verb|$+|:
\begin{minted}{perl}
"abc" =~ /^(.)(.)/;
say "first: $1; second: $2";
# first: a; second: b

"abc" =~ /^(?<first>.)(?<second>.)/;
say "first: $+{first}; second: $+{second}";
# first: a; second: b
\end{minted}
При этом к именованным захватам все еще можно обратиться по их номеру.

Существую также так называемые оглядывания (look around assertions), которые позволяют <<просматривать>> окружающий текст, но не включать его в захват (то есть имеют нулевую длину).
\begin{itemize}[nosep]
  \item (?=...) --- 0W+ вперёд
  \item (?!...) --- 0W- вперёд
  \item (?<=...) --- 0W+ назад
  \item (?<!...) --- 0W- назад
\end{itemize}
Например:
\begin{minted}{perl}
$_ = "foo bar baz";

say s{(\w+)(?=\s+)}{$1,}r; # foo, bar, baz
say s{(\s+)(?!bar)}{_}r; # foo bar_baz

say s{(?<= )(\w+)}{:$1}rg; # foo :bar :baz
say s{(?<! )(\w\w\w)}{[$1]}rg; # [foo] bar baz
\end{minted}
Заглядывать назад можно только на фиксированную длину.

\subsection{Модификаторы} %42 44:35
Модификатор, если приписать его к регулярному выражению, меняет его поведение:
\begin{itemize}
  \item Модификатор \verb|/s| (single line) приводит к тому, что в класс символов \verb|.| могут попадать переводы строк:
\begin{minted}{perl}
"\n" =~ /^.$/; # no match
"\n" =~ /^.$/s; # match

my $s = "line1\nline2\n";

$s =~ /line1.line2/; # no match
$s =~ /line1.line2/s; # match
\end{minted}
  \item Модификатор \verb|/m| (multiline) меняет поведение крышечки и доллара. Теперь: \verb|^|~--- начало каждой строки,  \verb|$|~--- конец каждой строки (до \verb|\n|). Например:
\begin{minted}{perl}
my $s = "sample\nstring";

$s =~ /^(.+)$/;    # no match
$s =~ /^(.+)$/m;   # "sample"
$s =~ /^(.+)$/ms;  # "sample\nstring"

$s =~ /^string/;   # no match
$s =~ /^string/m;  # matches "string"
\end{minted}
  \item Модификатор \verb|/i| (case insensitive) включает case-insensitive режим с учетом особенностей юникода:
\begin{minted}{perl}
my $s = "sample\nstring";

$s =~ /SAMPLE/;    # no match
$s =~ /SAMPLE/i;   # "sample"

# Unicode!

"tschüß" =~ /TSCHÜSS/i    # match. ß ↔ SS
"Σίσυφος" =~ /ΣΊΣΥΦΟΣ/i   # match. Σ ↔ σ ↔ ς
\end{minted}

\item Модификатор \verb|/x| (eXtended regexp) включает расширенный режим, в котором перестают интерпретироваться пробельные символы, а также появляется возможность добавлять комментарии:
\begin{minted}{perl}
$hexdig =~ m{
    ^ # begin of string
    (?:
        [0-9] # it could be digit
        |     # or
        [a-f] # letters from a to f
    )+ # several times
    $ # end of string
}sx;
\end{minted}

\item Модификатор \verb|/g| (global) приводит к тому, что при поиске и замене поиск и замена будет выполняться до тех пор, пока это возможно. В match в списковом контексте модификатор приводит к тому, что возвращается не первое попадание, а все.
\begin{minted}{perl}
my $s = "aaaa";
$s =~ s/a/b/;  # "baaa"
$s =~ s/a/b/g; # "bbbb"

@a = $a =~ /(.)/; # ('a')
@a = $a =~ /(.)/g; # ('a','a','a','a')

my $string = '~!@#$%^&*()';
$string =~ s{(.)}{\\$1}g;
# \~\!\@\#\$\%\^\&\*\(\)
\end{minted}

\item Модификатор \verb|/e| (eval) приводят к тому, что в регулярном выражении правая часть исполняется как код Perl. Следующее регулярное выражение позволяет преобразовать шестнадцатиричные числа в их десятичное значение:
\begin{minted}{perl}
my $string = '~!@#$%^&*()';

$string =~ s{(.)}{
    sprintf("U+%v04x;",$1)
}ge;
#U+007e;U+0021;U+0040;U+0023;U+0024;U+0025;
#U+005e;U+0026;U+002a;U+0028;U+0029;

my $nums = "0x123 123 0xff";
$nums =~ s{0x([\da-f]+)}{ hex($1) }ge;
say $nums; # 291 123 255
\end{minted}
Также существуют модификаторы \verb|/ee| (double eval), \verb|/eee| и так далее. 

\item Модификатор \verb|/a| (и \verb|/aa|) (ASCII-safe) меняет поведение \verb|\d|, \verb|\s|, \verb|\w| так, чтобы они подходили только под диапазон ASCII. \verb|/aa| приводит к тому, что все спец. символы, которые имеют представление в виде обычных символов, перестанут совпадать с ними.
\begin{minted}{perl}
use utf8;
use charnames ':full';
my $nums = "०१२३";
$nums =~ /\d/; # match
$nums =~ /\d/a; # no match

my $str = "\N{KELVIN SIGN}";
say $str =~ /K/i; # match
say $str =~ /K/ai; # match
say $str =~ /K/aai; # no match
\end{minted}

\end{itemize}

\subsection{Квантификаторы и жадность} % 53 1:02:32
По умолчанию в регулярном выражении квантификатор является жадным и старается захватить подстроку наибольшего размера. Для управления жадностью существует два флага квантификатора:
\begin{itemize}
  \item \verb|?| --- сделать нежадным (захватывать строку минимального размера).
  \item \verb|+| --- запрещает откат.
\end{itemize}

\begin{minted}{perl}
say "bc"    =~ /^(a*)b/;   # match, ""
say "abc"   =~ /^(a*)b/;   # match, "a"
say "aabc"  =~ /^(a*)b/;   # match, "aa"
say "aaabc" =~ /^(a*)b/;   # match, "aaa"

say "aaabc" =~ /^(a*)/;    # match, "aaa"
say "aaabc" =~ /^(a*?)/;   # match, ""
say "aaabc" =~ /^(a*?)a/;  # match, ""
say "aaabc" =~ /^(a*?)ab/; # match, "aa"

say "aaabc" =~ /^(a*+)/;   # match, "aaa"
say "aaabc" =~ /^(a*+)b/;  # match, "aaa"
say "aaabc" =~ /^(a*+)ab/; # no match

say "bc"    =~ /^(a+)b/;   # no match
say "abc"   =~ /^(a+)b/;   # match, "a"
say "aabc"  =~ /^(a+)b/;   # match, "aa"
say "aaabc" =~ /^(a+)b/;   # match, "aaa"

say "aaabc" =~ /^(a+)/;    # match, "aaa"
say "aaabc" =~ /^(a+?)/;   # match, "a"
say "aaabc" =~ /^(a+?)a/;  # match, "a"
say "aaabc" =~ /^(a+?)ab/; # match, "aa"

say "aaabc" =~ /^(a++)/;   # match, "aaa"
say "aaabc" =~ /^(a++)b/;  # match, "aaa"
say "aaabc" =~ /^(a++)ab/; # no match

say "bc"    =~ /^(a{1,2})b/; # no match
say "abc"   =~ /^(a{1,2})b/; # match, "a"
say "aabc"  =~ /^(a{1,2})b/; # match, "aa"
say "aaabc" =~ /^(a{1,2})b/; # no match

say "aaabc"  =~ /^(a{1,2})a/;  # match "aa"
say "aaabc"  =~ /^(a{1,2}?)a/; # match "a"
say "aabc"   =~ /^(a{1,2}?)b/; # match "aa"
\end{minted}

\subsection{Backreferencing и Global match}
Внутри регулярного выражения можно сослаться с помощью \verb|\gN|, \verb|\N| или \verb|\g{-N}| на предыдущий захват:
\begin{minted}{perl}
for (
    q{some with "quoted value" string},
    q{some with 'quoted " value' string},
) {
    say $2 if m{(["'])([^\g1]*)\g1};
}
# quoted value
# quoted " value

for ('e66e', 'f99f', 'z87z' ) {
    say $1 if m{(([a-z])(\d)\g{-1}\g{-2})}x;
}
#e66e
#f99f
\end{minted}

Помимо стандартного поведения Global match для того, чтобы заменить все или найти все. Обычный match в списковом контексте возвращает все совпадения, а в скалярном контексте, а условия в цикле --- это скалярный контекст, он делает следующую вещь.

У любой переменной строкового Perl есть внутреннее свойство --- позиция (можно прочитать и установить с помощью функции \verb|pos|), которое учитывается при исполнении регулярных выражений. Обычный match в скалярном контексте с модификатором g возвращает ровно одно совпадение, но оставляет позицию в той точке, в которой регулярное выражение остановилось. Следующая итерация данного регулярного выражения будет работать уже не с начала строки, а с этой позиции:
\begin{minted}{perl}
$_ = "abcd";
while (/(.)/g) {
    say $1, " ", pos($_);
    # a 1
    # b 2
    # c 3
    # d 4
}
say $1, " ", pos($_);
# undef, undef
\end{minted}
Когда регулярное выражение неуспешно, т.е. дошло до конца строки, значение pos сбрасывается в исходное значение undef. При следующем применении регулярного выражения поиск опять начнется с начала строки.

Для того, чтобы позиция не сбрасывалась, существует ключ \verb|/c|:
\begin{minted}{perl}
$_ = "abcd";
while (/(.)/gc) {
    say $1, " ", pos($_);
    # a 1
    # b 2
    # c 3
    # d 4
}
say $1, " ", pos($_);
# undef, 4
\end{minted}
В этом случае, если регулярное выражение неуспешно, позиция не меняется.

Еще один пример:
\begin{minted}{perl}
$_ = "abcdxcdcd";
while (/\G(.)/gc) {
    my $key = $1;
    my $pos = pos($_);
    if (/\Gcd/gc) {
        say "the key before cd is $key at $pos";
    } else {
        say "no cd next after $key";
    }
}
# no cd next after a
# the key before cd is b at 2
# the key before cd is x at 5
# no cd next after c
# no cd next after d
\end{minted}
Здесь /G интерпретируется как позиция, на которую указывает pos.

\subsection{Классы символов Unicode}
В unicode существую категории символов.
\begin{minted}{perl}
`\p{Category}` - совпадение с категорией
`\P{Category}` - исключение категории
`\N{SYMBOL NAME}` - точное имя (см. charnames)
\end{minted}

\begin{minted}{perl}
"UPPER" =~ /\p{IsUpper}/; # match
"UPPER" =~ /\P{IsUpper}/; # no match
"UPPER" =~ /\p{IsLower}/; # no match
"UPPER" =~ /\P{IsLower}/; # match
\end{minted}
Следующий код исключает все кавычки из текста, заменяя их на пробелы:
\begin{minted}{perl}
say q{«string"with'quotes»} =~
    s/\p{Quotation Mark}+/ /rg;
# ' string with quotes '
\end{minted}

Регулярные выражения в Perl, строго говоря, уже являются нерегулярными. Термин <<регулярное выражение>> пришел из математики. Такие выражения не должны быть рекурсивными, однако в Perl регулярные выражения были расширены и теперь можно использовать рекурсивные выражения. Название <<регулярное выражение>> закрепилось исторически.

\section{Отладка регулярных выражений}
В модуле re есть режим отладки:
\begin{minted}{perl}
use re 'debug';
\end{minted}
или же из командной строки:
\begin{minted}{bash}
perl -Mre=debug -E '"aaabc"   =~ /^(a{1,2}?)ab/;'
\end{minted}
При встрече регулярного выражения в этом случае будет выводиться отладочная информация:
\begin{minted}{bash}
Compiling REx "^(a{1,2}?)ab"
Final program:
   1: BOL (2)             # Beginning of line
   2: OPEN1 (4)           # Open group 1
   4:   MINMOD (5)        # Nongreedy (?)
   5:   CURLY {1,2} (9)   # Quantifier {}
   7:     EXACT <a> (0)
   9: CLOSE1 (11)
  11: EXACT <ab> (13)
  13: END (0)
\end{minted}
Регулярное выражение компилируется в псевдопрограмму на более-менее интуитивно понятном языке. После того, как регулярное выражение используется, выводится ход исполнения этой программы:
\begin{minted}{bash}
Guessed: match at offset 0
Matching REx "^(a{1,2}?)ab" against "aaabc"
   0 <> <aaabc>              |  1:BOL(2)
   0 <> <aaabc>              |  2:OPEN1(4)
   0 <> <aaabc>              |  4:MINMOD(5)
   0 <> <aaabc>              |  5:CURLY {1,2}(9)
                  EXACT <a> can match 1 times out of 1...
   1 <a> <aabc>              |  9:  CLOSE1(11)
   1 <a> <aabc>              | 11:  EXACT <ab>(13)
                    failed...
                  EXACT <a> can match 1 times out of 1...
   2 <aa> <abc>              |  9:  CLOSE1(11)
   2 <aa> <abc>              | 11:  EXACT <ab>(13)
   4 <aaab> <c>              | 13:  END(0)
Match successful!
\end{minted}
Это позволяет отлаживать регулярные выражения
